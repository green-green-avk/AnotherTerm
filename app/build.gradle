plugins {
    id 'com.android.application'
    id 'com.likethesalad.stem'
}

ext {
    versionCodeMain = 127
}

android {
    namespace 'green_green_avk.anotherterm'

    compileSdk 33
    ndkVersion '21.4.7075529'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId namespace
        minSdk 14
        targetSdk 31
        versionCode versionCodeMain
        versionName 'MkIIIv69-dev'
        vectorDrawables.useSupportLibrary = true

        resConfigs 'en'

        manifestPlaceholders += [namespace: namespace]

        buildConfigField 'String', 'NAMESPACE', "\"$namespace\""
        buildConfigField 'int', 'VERSION_CODE_MAIN', "$versionCodeMain"

        def SHELL_SESSION_TOKEN_VAR = 'SHELL_SESSION_TOKEN'
        buildConfigField 'String', 'SHELL_SESSION_TOKEN_VAR', "\"$SHELL_SESSION_TOKEN_VAR\""

        externalNativeBuild {
            cmake {
                cppFlags "-DSHELL_SESSION_TOKEN_VAR=\\\"$SHELL_SESSION_TOKEN_VAR\\\""
                arguments "-DAPP_ID_BASE=\\\"${applicationId}\\\""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
        }
    }

    applicationVariants.all { variant ->
        variant.resValue 'string', 'versionName', variant.versionName
        variant.resValue 'string', 'applicationId', variant.applicationId
        variant.buildConfigField 'int', 'TARGET_SDK_VERSION',
                "${variant.mergedFlavor.targetSdkVersion.apiLevel}"
    }

    // TODO: Find some library-side approach...
    // * Gradle + AGP looks crappy when used in even a bit irregular way...
    // https://stackoverflow.com/questions/44731997/android-gradle-packagingoptions-pickfirst-and-exclude-not-working
    packagingOptions {
        resources {
            excludes += ['org/bouncycastle/pqc/**', 'org/bouncycastle/x509/**']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                    'proguard-rules-debug.pro'
            externalNativeBuild {
                cmake {
                    arguments "-DAPP_ID_TYPE=\\\"${applicationIdSuffix}\\\""
                }
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel 'FULL'
            externalNativeBuild {
                cmake {
                    arguments '-DAPP_ID_TYPE=\\"\\"'
                }
            }
        }
    }

    flavorDimensions 'compat'

    productFlavors {
        googleplay {
            // Purely for Google Play publishing
            // (to comply with possible future app-bundle requirement)
            dimension 'compat'
            externalNativeBuild.cmake.arguments '-DAPP_ID_SDK=\\"\\"'
        }
        redist {
            // Redistributable downgradable all-in-one apk
            dimension 'compat'
            versionCode 90 // Fixed to enable downgradability
            applicationIdSuffix '.redist'
            externalNativeBuild.cmake.arguments "-DAPP_ID_SDK=\\\"${applicationIdSuffix}\\\""
        }
        oldgood {
            // Redistributable downgradable all-in-one apk with target API 28
            dimension 'compat'
            versionCode 90 // Fixed to enable downgradability
            applicationIdSuffix '.oldgood'
            targetSdk 28
            externalNativeBuild.cmake.arguments "-DAPP_ID_SDK=\\\"${applicationIdSuffix}\\\""
        }
    }

    sourceSets {
        googleplay {
            java.srcDirs += ['src/modern-extras/java']
            res.srcDirs += ['src/modern-extras/res']
        }
        redist {
            java.srcDirs += ['src/modern-extras/java', 'src/sideload-extras/java']
            res.srcDirs += ['src/modern-extras/res', 'src/sideload-extras/res']
        }
        oldgood {
            java.srcDirs += ['src/sideload-extras/java']
            res.srcDirs += ['src/sideload-extras/res']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'me.saket:better-link-movement-method:2.2.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation project(path: ':PngChunkExtractor')
    implementation project(path: ':CompatColorPicker')
    implementation project(path: ':MaterialSeekBarPreference')

    implementation 'commons-io:commons-io:2.11.0'
    //noinspection GradleDependency
    implementation('org.apache.commons:commons-text:1.9') {
        because '''1.10.0:
`java.util.stream` does not exist in API < 24.
Some desugaring is required... postponed for better times.
See <https://developer.android.com/studio/write/java8-support#library-desugaring>'''
    }
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'

    implementation project(path: ':PtyProcess')
    implementation 'com.github.felHR85:UsbSerial:6.1.0'
    implementation project(path: ':bluetoothspp')
    implementation project(path: ':jsch')
    implementation project(path: ':TelnetClient')

    implementation 'com.github.green-green-avk:libusbmanager:1.7'

    implementation 'com.github.green-green-avk:anothertermshellpluginutils:1.16'
    implementation 'com.github.green-green-avk:anothertermshellpluginutils-side-application:1.1'

    // Per-flavor
    redistImplementation project(path: ':UserCertificatesEnabler')
    oldgoodImplementation project(path: ':UserCertificatesEnabler')
    redistImplementation project(path: ':USBCameraWorkaround')
    oldgoodImplementation project(path: ':USBCameraWorkaround')
}
